plugins {
    // Apply the plugin. You can find the latest version at https://github.com/neoforged/ModDevGradle/packages/2159800.
    id 'net.neoforged.moddev' version '0.1.90'
}

base.archivesName = "${mod_name}-neoforge-${minecraft_version}"

neoForge {
    version = "${neoforge_version}"
    parchment {
        // Get versions from https://parchmentmc.org/docs/getting-started
        // Omit the "v"-prefix in mappingsVersion
        minecraftVersion = "${parchment_mc_version}"
        mappingsVersion = "${parchment_version}"
    }
	
    runs {
        client {
            client()
        }
        data {
            data()
        }
        server {
            server()
        }
    }

    mods {
        mct {
            sourceSet sourceSets.main
        }
    }
}

dependencies {
    compileOnly project(':Common')
    compileOnly 'com.google.auto.service:auto-service:1.0.1'
    annotationProcessor 'com.google.auto.service:auto-service:1.0.1'
}

tasks.compileJava {
    source(project(':Common').sourceSets.main.allSource)
}

final expandProps = [
        "version": project.version,
        "modid": project.mod_id
]

tasks.processResources {
    from(project(':Common').sourceSets.main.resources)
    inputs.properties(expandProps)

    filesMatching('META-INF/neoforge.mods.toml') {
        expand expandProps
    }
}
